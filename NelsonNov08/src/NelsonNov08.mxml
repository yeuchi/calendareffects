<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" 
				xmlns:views="views.*" 
				xmlns:services="com.ctyeung.services.*"
				layout="absolute" 
				backgroundColor="0xFBD2F7"
				creationComplete="onCreationComplete()" viewSourceURL="srcview/index.html">
	<mx:Style>
        @font-face {
            src: local("Comic Sans MS");
            fontFamily: EmbeddedComicSansMS;
            fontWeight: normal;
        }
    </mx:Style>
    
    	<mx:Script>
		<![CDATA[
			import com.ctyeung.transition.Rotate3D;
			import com.ctyeung.tile.CImage;
			import mx.core.UIComponent;
			import mx.effects.Parallel;
			import mx.events.EffectEvent;
			import mx.effects.Rotate;
			import mx.effects.Resize;
			import mx.containers.Box;
			import mx.containers.VBox;
			import mx.containers.Canvas;
			import mx.controls.Image;
			import mx.containers.Panel;
			
			protected var index:int = 0;
			protected var imgList:Array;
			protected var posList:Array;
			[Bindable]protected var _wid:int;
			[Bindable]protected var _len:int;
			[Bindable]protected var _thumbWid:int = 100;
			[Bindable]protected var _duration:int = 2000;
			protected var bFirstTime:Boolean = true;
			protected var parallel:Parallel;
			protected var rotate:Rotate3D;
			protected var rotateIn:Rotate3D;

			protected function onCreationComplete():void
			{
				var pt:Point = new Point(10, 140);
				posList = new Array();
				posList.push(pt);
				
				rotate = new Rotate3D();
				rotateIn = new Rotate3D();
			}
			
			private function onLoadCompleted(e:Event):void
			{
				imgList = photoService.imgList;	
				var img:Image = imgList[index];
				img.id = index.toString();
				img.width = _thumbWid;
				img.height = _thumbWid;
				img.buttonMode=true;
				img.useHandCursor=true;
				img.addEventListener(MouseEvent.CLICK, onClickImage);
				img.addEventListener(MouseEvent.MOUSE_OUT, onMouseOut);
				img.addEventListener(MouseEvent.MOUSE_OVER, onMouseOver);
				
				var pt:Point = posList[index];
				img.x = pt.x;
				img.y = pt.y;
				
				var newPt:Point = new Point();
				
				newPt.x = pt.x + _thumbWid + 10;
				newPt.y = pt.y;
				
				if ( ( newPt.x + _thumbWid ) >=  this.width ) {
					newPt.x = 10;
					newPt.y = pt.y + _thumbWid + 10;
				}
				posList.push(newPt);
				this.addChild(img);
				
				rotate.target = img;
				rotate.angleXTo = Math.random() * 360;
				rotate.angleYTo = Math.random() * 360;
				rotate.angleZTo = Math.random() * 360;
				rotate.end();
				rotate.play();
				index ++;
				
				if ( index == photoService.photoFeed.length) {
					index = 0;
					onMoveStart();
				}
				_wid = this.width;
				_len = this.height;
			}
			
			private function onMouseOut(e:MouseEvent):void
			{
				var img:Image = e.currentTarget as Image;
				img.alpha = 1;
			}
			
			private function onMouseOver(e:MouseEvent):void
			{
				var img:Image = e.currentTarget as Image;
				img.alpha = .6;
			}
			
			private function onMoveStart():void
			{
				parallel = new Parallel();
				parallel.addEventListener(EffectEvent.EFFECT_END, onReStart);
				
				if ( index >= imgList.length)
					index =0;
				
				this.setChildIndex(imgList[index], this.numChildren-1);
				var last:int = (index==0)?imgList.length-1:index-1;
				
				if ( !bFirstTime )
				{
					var pt:Point = posList[last];
					moveOut.target = imgList[last];
					moveOut.xTo = pt.x;
					moveOut.yTo = pt.y;
					moveOut.xFrom = _wid/2-_thumbWid;
					moveOut.yFrom = _len/2-_thumbWid;
					sizeSmall.target = imgList[last];
					parallel.addChild(sizeSmall);
					parallel.addChild(moveOut);
				}
				var img:Image = imgList[index];
				var ptIn:Point = posList[index];
				moveIn.target = img;
				moveIn.xFrom = ptIn.x;
				moveIn.yFrom = ptIn.y;
				moveIn.xTo = _wid/2-_thumbWid;
				moveIn.yTo = _len/2-_thumbWid;
				sizeBig.target = img;
				
				if ( !bFirstTime )
				{
					rotate.duration = _duration;
					rotate.target = imgList[last];
					rotate.angleXTo = 0;
					rotate.angleYTo = 0;
					rotate.angleZTo = 0;
					rotate.angleXFrom = 0;
					rotate.angleYFrom = 0;
					rotate.angleZFrom = 0;
					parallel.addChild(rotate);
				}
				rotateIn.duration = _duration;
				rotateIn.target = img;
				rotateIn.angleXTo = 0;
				rotateIn.angleYTo = 0;
				rotateIn.angleZTo = 0;
				rotateIn.angleXFrom = Math.random() * 360;
				rotateIn.angleYFrom = Math.random() * 360;
				rotateIn.angleZFrom = Math.random() * 360;
				
				parallel.addChild(moveIn);
				parallel.addChild(sizeBig);
				parallel.addChild(rotateIn);
				parallel.end();
				parallel.play();
				
				index ++;
				bFirstTime = false;
			}
			
			private function onReStart(e:Event):void
			{
				parallel.removeEventListener(EffectEvent.EFFECT_END, onReStart);
				onMoveStart();
			}
			
/////////////////////////////////////////////////////////////////////
// events
			
			private function onClickImage(e:MouseEvent):void
            {
            	var img:CImage = e.currentTarget as CImage;
            	var url:String = img.url;
            	//url = url.toLowerCase();
            	url = url.replace("small/", "big/");
            	url = url.replace("Small", "");
            	url = url.replace("jpg", "JPG");
            	navigateToURL(new URLRequest(url), "_blank");
            }
            
            private function onClickSlideShow():void
            {
            	navigateToURL(new URLRequest("http://www.ctyeung.com/flex/gallery/FlexEx1.html"), "_blank");
            }
            
            private function onMouseOverLabel():void
            {
            	lblSlide.alpha = .5;
            }
            
            private function onMouseOutLabel():void
            {
            	lblSlide.alpha = 1;
            }
		]]>
	</mx:Script>
	
	<mx:Resize id="sizeBig" duration="{_duration}" widthFrom="{_thumbWid}" heightFrom="{_thumbWid}" widthTo="200" heightTo="200"/>
	<mx:Resize id="sizeSmall" duration="{_duration}" widthFrom="200" heightFrom="200" widthTo="{_thumbWid}" heightTo="{_thumbWid}"/>
	<mx:Move id="moveIn" duration="{_duration}" />
	<mx:Move id="moveOut" duration="{_duration}" />
	
	<services:PhotoService id="photoService" loadCompleted="onLoadCompleted(event)"/>
	<mx:Canvas>
	<mx:Label id="lblMonth" text="November" 
					fontSize="42" fontFamily="EmbeddedComicSansMS" color="0x9900FF" 
					x="{this.width/2-lblMonth.width/2}" y="10"/>
	</mx:Canvas>
		<mx:Label 	id="lblSlide" text="SlideShow" click="onClickSlideShow()" 
					buttonMode="true" useHandCursor="true" x="10" y="20"
					fontSize="20" fontFamily="EmbeddedComicSansMS" color="0x9900FF" 
					mouseOut="onMouseOutLabel()" mouseOver="onMouseOverLabel()"/>
</mx:Application>
